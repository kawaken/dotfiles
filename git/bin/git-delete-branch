#!/usr/bin/env zsh

sort_order="oldest"
while [[ $# -gt 0 ]]; do
    case "$1" in
        -o|--oldest) sort_order="oldest"; shift ;;
        -a|--alpha) sort_order="alpha"; shift ;;
        *) shift ;;
    esac
done

main_branch=$(git symbolic-ref refs/remotes/origin/HEAD 2>/dev/null | sed 's@^refs/remotes/origin/@@' || echo "main")

get_branches_with_status() {
    local current_branch=$(git branch --show-current)
    local worktree_branches=(${(f)"$(git worktree list --porcelain | grep '^branch ' | sed 's|^branch refs/heads/||')"})
    local main_log=$(git log "$main_branch" --oneline -100)

    local sort_key
    case "$sort_order" in
        oldest) sort_key="committerdate" ;;
        alpha) sort_key="refname" ;;
    esac

    local branches=(${(f)"$(git for-each-ref --sort=$sort_key refs/heads/ --format='%(committerdate:short) %(refname:short)')"})

    for line in $branches; do
        local date=${line%% *}
        local branch=${line#* }

        [[ "$branch" == "main" || "$branch" == "master" ]] && continue
        [[ "$branch" == "$current_branch" ]] && continue
        (( ${worktree_branches[(I)$branch]} )) && continue

        if git merge-base --is-ancestor "$branch" "$main_branch" 2>/dev/null; then
            print -P "%F{green}✓%f $date $branch"
        elif echo "$main_log" | grep -q "$branch"; then
            print -P "%F{green}✓%f $date $branch"
        else
            print -P "%F{240}?%f $date $branch"
        fi
    done
}

selected=$(get_branches_with_status | \
    fzf --ansi \
        --multi \
        --bind 'ctrl-a:select-all' \
        --height=50% \
        --layout=reverse \
        --header='Tab: toggle | Ctrl-A: select all | Enter: delete')

[ -z "$selected" ] && exit 0

branches=(${(f)"$(echo "$selected" | awk '{print $3}')"})
for branch in $branches; do
    git branch -D "$branch"
done
