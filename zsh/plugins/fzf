[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

fzf-history-widget-accept() {
  fzf-history-widget
  zle accept-line
}
zle     -N     fzf-history-widget-accept
bindkey '^X^R' fzf-history-widget-accept

__fgitst() {
  git rev-parse --is-inside-work-tree > /dev/null 2>&1 || return

  local cmd="git status -s"
  setopt localoptions pipefail no_aliases 2> /dev/null
  local item
  eval "$cmd" | FZF_DEFAULT_OPTS="--height ${FZF_TMUX_HEIGHT:-40%} --reverse --bind=ctrl-z:ignore $FZF_DEFAULT_OPTS $FZF_CTRL_T_OPTS" $(__fzfcmd) -m "$@" | while read item; do
    echo -n "${(q)item:3} "
  done
  local ret=$?
  echo
  return $ret
}

fzf-git-status() {
  LBUFFER="${LBUFFER}$(__fgitst)"
  local ret=$?
  zle reset-prompt
  return $ret
}
zle     -N   fzf-git-status
bindkey '^S' fzf-git-status
